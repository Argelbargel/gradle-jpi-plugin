plugins {
    id 'groovy'
    id 'maven-publish'
    id 'codenarc'
    id 'com.gradle.plugin-publish' version '0.9.6'
    id 'java-gradle-plugin'
}

repositories {
    // using JCenter for dependency resolution is recommended, see https://plugins.gradle.org/docs/publish-plugin
    jcenter()
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
    compile gradleApi()
    compile 'org.jvnet.localizer:maven-localizer-plugin:1.13'
    compile 'org.jenkins-ci:version-number:1.0'
    compile 'net.java.sezpoz:sezpoz:1.12'
    compile localGroovy()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all' // use the version that is distributed with Gradle
    }
    testCompile 'org.xmlunit:xmlunit-core:2.3.0'
}

artifacts {
    archives jar
}


publishing {
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

codenarc {
    toolVersion = '0.26.0'
    configFile = file('config/codenarc/rules.groovy')
}

codenarcTest {
    configFile = file('config/codenarc/rules-test.groovy')
}

test {
    jvmArgs += ['-XX:MaxPermSize=256m']
}

group = 'argelbargel.gradle.plugins'
archivesBaseName = 'gradle-jpi-plugin'
description = 'Gradle plugin for building and packaging Jenkins plugins'
if (project.hasProperty('release') && project.getProperty('release') != '') {
    version = project.getProperty('release')
}


pluginBundle {
    website = 'https://wiki.jenkins-ci.org/display/JENKINS/Gradle+JPI+Plugin'
    vcsUrl = 'https://github.com/jenkinsci/gradle-jpi-plugin'
    description = 'A plugin for building Jenkins plugins'
    tags = ['jenkins']

    plugins {
        jpiPlugin {
            id = 'org.jenkins-ci.jpi'
            displayName = 'JPI plugin'
        }
    }
}